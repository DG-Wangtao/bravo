apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.king.bravo'

buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		// Resolve shadow plugin https://github.com/johnrengelman/shadow
		classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
	}
}

repositories {
	mavenCentral()
}

ext.versions = [
	flink		: '1.6.+',
	slf4j		: '1.7.+',
	logback		: '1.2.+',
	junit		: '4.+',
]

configurations.all {
	exclude module: 'commons-logging'
	exclude module: 'log4j'
	exclude module: 'slf4j-log4j12'
}

dependencies {
    compile "com.google.guava:guava:18.0"
	compileOnly "org.apache.flink:flink-streaming-java_2.11:${versions.flink}"
	compileOnly "org.apache.flink:flink-statebackend-rocksdb_2.11:${versions.flink}"
	compileOnly "org.apache.flink:flink-clients_2.11:${versions.flink}"

	testCompile "junit:junit:${versions.junit}"
	testCompile "org.apache.flink:flink-runtime_2.11:${versions.flink}:tests"
	testCompile "org.apache.flink:flink-statebackend-rocksdb_2.11:${versions.flink}"
	testCompile "org.apache.flink:flink-test-utils_2.11:${versions.flink}"

	testRuntime "ch.qos.logback:logback-classic:${versions.logback}"
	testRuntime "org.slf4j:log4j-over-slf4j:${versions.slf4j}"
	testRuntime "org.slf4j:jcl-over-slf4j:${versions.slf4j}"
}

wrapper {
	gradleVersion = 4.7
}

shadowJar {
    // The jar has tons of files, causing "archive contains more than 65535 entries" without this,
    // which enables Zip64 extension (https://en.wikipedia.org/wiki/Zip_(file_format)#ZIP64).
    zip64 = true

    // Do not copy the signatures in the META-INF folder. Otherwise,
    // this might cause SecurityExceptions when using the JAR.
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/LICENSE'
}
